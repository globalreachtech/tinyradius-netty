plugins {
    id 'java-library'
    id 'jacoco'
    id 'signing'
    id 'maven-publish'
    id 'io.freefair.lombok' version '8.10.2'
    id 'org.sonarqube' version '5.1.0.4882'
    id 'io.github.gradle-nexus.publish-plugin' version '2.0.0'
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

group = 'com.globalreachtech'
version = '2.1.1-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.apache.logging.log4j:log4j-api:2.24.1'
    implementation 'io.netty:netty-codec:4.1.115.Final'
    implementation 'jakarta.xml.bind:jakarta.xml.bind-api:4.0.2'

    testImplementation 'org.apache.logging.log4j:log4j-core:2.24.1'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.11.3'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.14.2'
    testImplementation 'org.awaitility:awaitility:4.2.2'
    testImplementation 'org.assertj:assertj-core:3.26.3'
    testImplementation('net.jradius:jradius-core:1.1.5') {
        transitive = false // for reference implementation of hashing utilities
    }
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

jacocoTestReport {
    reports {
        xml.required = true
        html.required = true
    }
}

sonarqube {
    properties {
        property "sonar.projectKey", "globalreachtech_tinyradius-netty"
        property "sonar.organization", "globalreachtech"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.exclusions", "'**/AttributeType*','**/PacketType*'"
    }
}

test {
    testLogging.showStandardStreams = true
    testLogging.exceptionFormat = 'full'
    useJUnitPlatform()
}

tasks.register('javadocJar', Jar) {
    archiveClassifier = 'javadoc'
    from javadoc
}

tasks.register('sourcesJar', Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

nexusPublishing {
    repositories {
        sonatype()
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            afterEvaluate {
                artifact sourcesJar
                artifact javadocJar
            }
            pom {
                name = project.name
                description = 'TinyRadius-Netty is a small Java Radius library'
                url = 'https://github.com/globalreachtech/tinyradius-netty'
                licenses {
                    license {
                        name = 'GNU Lesser General Public License, version 2.1'
                        url = 'https://www.gnu.org/licenses/old-licenses/lgpl-2.1.html'
                    }
                }
                developers {
                    developer {
                        id = "horaceli"
                        url = "https://github.com/horaceli"
                    }
                    developer {
                        id = 'globalreachtech'
                        organizationUrl = 'https://www.globalreachtech.com/'
                    }
                }
                scm {
                    url = 'https://github.com/globalreachtech/tinyradius-netty'
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}
