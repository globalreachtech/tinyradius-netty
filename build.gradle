plugins {
    id 'java-library'
    id 'maven-publish'
    id 'jacoco'
    id 'signing'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8
def versionName = '1.5.10-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.apache.logging.log4j:log4j-api:2.16.0'
    implementation 'io.netty:netty-all:4.1.70.Final'
    implementation 'javax.xml.bind:jaxb-api:2.3.1'

    testImplementation 'org.apache.logging.log4j:log4j-core:2.16.0'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.2'
    testImplementation 'org.mockito:mockito-junit-jupiter:4.1.0'
    testImplementation 'org.awaitility:awaitility:4.1.1'
    testImplementation('net.jradius:jradius-core:1.1.5') {
        transitive = false // for reference implementation of hashing utilities
    }
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

test {
    testLogging.showStandardStreams = true
    testLogging.exceptionFormat = 'full'
    useJUnitPlatform()
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            afterEvaluate {
                artifact sourcesJar
                artifact javadocJar
            }
            groupId 'com.globalreachtech'
            artifactId 'tinyradius-netty'
            version versionName
            pom {
                name = 'tinyradius-netty'
                description = 'TinyRadius-Netty is a small Java Radius library'
                url = 'https://github.com/globalreachtech/tinyradius-netty'
                licenses {
                    license {
                        name = 'GNU Lesser General Public License, version 2.1'
                        url = 'https://www.gnu.org/licenses/old-licenses/lgpl-2.1.html'
                    }
                }
                developers {
                    developer {
                        id = "horaceli"
                        url = "https://github.com/horaceli"
                    }
                    developer {
                        id = 'globalreachtech'
                        organizationUrl = 'https://www.globalreachtech.com/'
                    }
                }
                scm {
                    url = 'https://github.com/globalreachtech/tinyradius-netty'
                }
            }
        }
    }
    repositories {
        maven {
            credentials{
                username System.getenv('SONATYPE_USER')
                password System.getenv('SONATYPE_PASSWORD')
            }
            url = version.endsWith('SNAPSHOT') ? "https://oss.sonatype.org/content/repositories/snapshots" : "https://oss.sonatype.org/service/local/staging/deploy/maven2"
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

