plugins {
    id 'java-library'
    id 'jacoco'
    id 'com.github.kt3k.coveralls' version '2.8.4'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.8.4'
    id 'com.jfrog.artifactory' version '4.9.8'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.slf4j:slf4j-api:1.7.28'
    implementation 'io.netty:netty-all:4.1.39.Final'
    implementation 'javax.xml.bind:jaxb-api:2.3.1'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.5.1'
    testImplementation 'org.slf4j:slf4j-simple:1.7.28'
    testImplementation('net.jradius:jradius-core:1.1.5') {
        transitive = false // for reference implementation of hashing utilities
    }
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

coveralls {
    jacocoReportPath 'build/reports/jacoco/test/jacocoTestReport.xml'
}

javadoc {
    destinationDir file('docs')
    options.noTimestamp = true
    options.addBooleanOption('html5', true)  // comment out if building with <jdk9
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

bintray {
    user = ''
    key = ''
    publications = ['mavenJava']
    pkg {
        repo = 'grt-maven'
        name = 'tinyradius-netty'
        userOrg = 'globalreachtech'
        licenses = ['LGPL-2.1']
        vcsUrl = 'https://github.com/globalreachtech/tinyradius-netty.git'
        version {
            name = '1.0.8'
            released = new Date()
//            mavenCentralSync {
//                user = 'userToken' //OSS user token
//                password = 'paasword' //OSS user password
//            }

        }
    }
}

def pomConfig = {
    name 'tinyradius-netty'
    description 'TinyRadius is a simple, small and fast Java Radius library capable of sending and receiving Radius packets of all types.'
    url 'https://github.com/globalreachtech/tinyradius-netty'
    licenses {
        license {
            name 'GNU Lesser General Public License, version 2.1'
            url 'https://www.gnu.org/licenses/old-licenses/lgpl-2.1.html'
            distribution "repo"
        }
    }
    developers {
        developer {
            id 'wuttke'
            name 'Matthias Wuttke'
            email 'mw@teuto.net'
        }
        developer {
            id 'ctran'
            email 'cuong.tran@gmail.com'
        }
        developer {
            id 'globalreachtech'
            organization 'GlobalReach Technology'
            organizationUrl 'https://www.globalreachtech.com/'
        }
    }
    scm {
        url 'https://github.com/globalreachtech/tinyradius-netty'
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            afterEvaluate {
                artifact sourcesJar
                artifact javadocJar
            }
            groupId 'com.globalreachtech'
            artifactId 'tinyradius-netty'
            version '1.0.8'
            pom.withXml {
                asNode().children().last() + pomConfig
            }
        }
    }
}

